plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.8.4"
}

repositories {
}

if (file("${rootDir}/${System.getenv("CONFIG")}").exists())
	apply from: "${rootDir}/${System.getenv("CONFIG")}"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name + "-" + project.mod_version + "+" + project.version_meta
group = project.maven_group

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_KEY")
	publications = ['SpinneryPublication']
	pkg {
		repo = 'Spinnery'
		name = archives_base_name
		userOrg = 'spinnery'
		licenses = ['LGPL-3.0']
		vcsUrl = 'https://github.com/vini2003/Spinnery.git'
		version {
			name = project.mod_version + "+" + project.version_meta
			released  = new Date()
			vcsTag = project.mod_version + "+" + project.version_meta
		}
	}
}

minecraft {
}

dependencies {
	// Minecraft
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	// Yarn
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	// Fabric Loader
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Jankson
	modCompile "blue.endless:jankson:1.2.0"
	include "blue.endless:jankson:1.2.0"

	// ModMenu
	modImplementation "io.github.prospector:modmenu:${modmenu_version}"
}

processResources {
	inputs.property "mod_version", mod_version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "mod_version": mod_version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		SpinneryPublication(MavenPublication) {
			groupId 'com.github.vini2003'
			artifactId 'spinnery'
			version project.mod_version + "+" + project.version_meta

			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact javadocJar
		}
	}

	repositories {
		mavenLocal()
	}
}

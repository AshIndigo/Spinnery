buildscript {
    repositories {
        maven {
            name = "Forge"
            url = "https://files.minecraftforge.net/maven"
        }

        maven {
            name = "Sponge"
            url= "https://dist.creeper.host/Sponge/maven"
        }

        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "org.spongepowered.mixin"

version = "${mod_version}"
group = 'com.vini2003.spinnery'
archivesBaseName = "${modid}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven {
        name = "Sponge"
        url= "https://dist.creeper.host/Sponge/maven"
    }

    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:1.15.2-31.1.63"

    implementation "org.spongepowered:mixin:0.8"

    implementation "blue.endless:jankson:1.1.0"
}

minecraft {
    mappings channel: 'snapshot', version: '20200503-1.15.1'

    runs {
        client {
            workingDirectory project.file('run')

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/')

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${modid}",
                "Specification-Vendor": "examplemodsareus",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"examplemodsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${modid}.mixins.json"
        ])
    }
}

shadowJar {
    manifest {
        inheritFrom project.jar.manifest
    }

    dependencies {
        include(dependency("blue.endless:jankson:1.1.0"))
    }

    baseName = 'spinnery'
    appendix = ''
    classifier = ''
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

reobf {
    shadowJar {}
}

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.build.dependsOn shadowJar

publishing {
    tasks.publish.dependsOn 'build'

    publications {
        mavenJava(MavenPublication) {
            artifactId 'spinnery'
            groupId 'com.github.vini2003'
            version '2.0.31+forge-1.15.2'

            artifact shadowJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ['mavenJava']
    pkg {
        repo = 'Spinnery'
        name = 'spinnery'
        userOrg = 'spinnery'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/vini2003/Spinnery.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}